plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.95'
}

neoForge.version = "21.6.11-beta"
version = "1.8.0"
group = "dev.gigaherz.eyes"
base.archivesName = "EyesInTheDarkness-${neoForge.minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    runs {
        // applies to all the run configs below
        configureEach {
            gameDirectory = project.file('run')
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
        }

        server {
            server()
        }
    }

    parchment {
        minecraftVersion="1.21.5"
        mappingsVersion="2025.06.01"
    }

    mods {
        thismod {
            sourceSet(sourceSets.main)
        }
        debugstuff {
            sourceSet(sourceSets.test)
        }
    }
}

sourceSets.test {
    compileClasspath += sourceSets.main.compileClasspath
}

jar {
    from(files('LICENSE.txt'))

    manifest {
        attributes([
                "Specification-Title": "eyes",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
